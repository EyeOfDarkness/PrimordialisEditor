buildscript{
    repositories{
        mavenLocal()
        mavenCentral()
        maven{ url 'https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository' }
        maven{ url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven{ url "https://jitpack.io" }
    }

    ext{
        arcHash = "v146"
        
        arcModule = { String name ->
            //skip to last submodule
            name = name.substring(name.lastIndexOf(':') + 1)
            return "com.github.Anuken.Arc:$name:$arcHash"
        }
    }

    dependencies{
        classpath "com.github.Anuken.Arc:packer:$arcHash"
        classpath "com.github.Anuken.Arc:arc-core:$arcHash"
    }
}

allprojects{
    apply plugin: "java"

    version = 'dev'
    
    targetCompatibility = 8
    sourceCompatibility = JavaVersion.VERSION_17

    ext{
        appName = "PrimEditor"
        
        //arcModule = { String name -> return project(":Arc:$name") }
    }

    repositories{
        mavenLocal()
        mavenCentral()
        maven{ url 'https://raw.githubusercontent.com/Zelaux/MindustryRepo/master/repository' }
        maven{ url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven{ url "https://oss.sonatype.org/content/repositories/releases/" }
        maven{ url 'https://jitpack.io' }
    }
    
    dependencies{
        annotationProcessor "com.github.Anuken:jabel:93fde537c7"
    }
    
    tasks.withType(JavaCompile){
        sourceCompatibility = JavaVersion.VERSION_17
        options.release = 8
    }
}

project(":core"){
    apply plugin: "java-library"

    dependencies{
        api arcModule("arc-core")
        api arcModule("extensions:freetype")
        api arcModule("extensions:flabel")
        api 'org.lz4:lz4-pure-java:1.8.0'
    }
}

project(":desktop"){
    apply plugin: "java"

    dependencies{
        implementation project(":core")
        implementation arcModule("backends:backend-sdl")
        implementation arcModule("natives:natives-desktop")
        implementation arcModule("natives:natives-freetype-desktop")
    }
}